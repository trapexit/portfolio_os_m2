\ @(#) fixedmono8.ins 95/11/16 1.8
\ $Id: fixedmono8.ins,v 1.7 1995/01/20 21:27:52 peabody Exp $
\ 8 bit monophonic sample player
\
\ 930420 PLB Fix reference to InFIFO, was FIFOIn0 !!
\
\ By: Phil Burk
\ Copyright 3DO 1992

\\\	NOAUTODOC -private -class DSP -name fixedmono8.dsp
\\\	OBSOLETE - Play a monophonic 8-bit sample at 44,100 Hz.
\\\
\\\	  Function
\\\
\\\	    This instrument plays a monophonic 8-bit sample at 44,100 Hz.
\\\
\\\	  Knobs -enumerated
\\\
\\\	    Amplitude
\\\	        0..0x7FFF. Defaults to 0x7FFF.
\\\
\\\	  Outputs
\\\
\\\	    Output
\\\
\\\	  Input FIFOs
\\\
\\\	    InFIFO
\\\
\\\	  Resources
\\\
\\\	    Ticks: 23
\\\
\\\	    Code: 21 words
\\\
\\\	  Implementation
\\\
\\\	    V20
\\\
\\\	  See Also
\\\
\\\	    halfmono8.dsp, varmono8.dsp, fixedstereo8.dsp
\\\

include? DFID_FIXEDMONO8 function_ids.j
anew task-fixedmono8.ins

_RED
dspp{
ins{

DFID_FIXEDMONO8 dspp-function-id !
$" fixedmono8.dsp" ins-name !

ALLOC.INFIFO  InFIFO
ALLOC.IMEM    Toggle \ are we on odd or even byte
ALLOC.IMEM    SampleHold   \ hold sample from FIFO
ALLOC.OUTPUT Output
0 $ 7FFF $ 7FFF ALLOC.KNOB Amplitude
23 ALLOC.TICKS

	InFIFO FIFOSTATUS+ _A #$ 0000 _+	\ address of FIFO status
	_BEQ: @NODATA						\ branch to be offset

\ -------------------------------------------
	Toggle _%A #$ 8000 _+   \ alternate between neg and pos
	_BMI: @GetNewSamp
\ Process held sample
	SampleHold _A 8 _<<' _TRL  \ shift into high position
	_JUMP: @Done

_LABEL: @GetNewSamp
	SampleHold _A  _= InFIFO _A _TRL    \ get 16 bits from FIFO \ 930420
	ACCUME #$ FF00 _AND               \ mask off high byte

_LABEL: @Done

	Output _A _= ACCUME Amplitude _A _*	\  to Mixer

_LABEL: @NODATA

}ins
}dspp
