\ @(#) halfmonosample.ins 95/11/16 1.8
\ $Id: halfmonosample.ins,v 1.6 1994/08/18 20:01:44 peabody Exp $
\ half sample rate, 16 bit monophonic sample player
\
\ 2 States
\    0  => get new 16 bit word,  interpolated prev -> new
\    1  => play new
\
\ By: Phil Burk
\ Copyright 3DO 1992
\
\ 940608 PLB removed FIFOSTATUS check
\            reduced tick allocation from 30 to 21
\            reduced code size from 28 to 23

\\\	NOAUTODOC -private -class DSP -name halfmonosample.dsp
\\\	OBSOLETE - Play a monophonic sample at 22,050 hertz.
\\\
\\\	  Function
\\\
\\\	    This instrument plays a monophonic sample recorded at 22,050 hertz.
\\\
\\\	  Knobs
\\\
\\\	    Amplitude
\\\	        0..0x7FFF. Defaults to 0x7FFF.
\\\
\\\	  Outputs -enumerated
\\\
\\\	    Output
\\\
\\\	  Input FIFOs -enumerated
\\\
\\\	    InFIFO
\\\
\\\	  Resources
\\\
\\\	    Ticks: 21
\\\
\\\	    Code: 23 words
\\\
\\\	  Implementation
\\\
\\\	    V20
\\\
\\\	  See Also
\\\
\\\	    fixedmonosample.dsp, sampler.dsp, halfstereosample.dsp
\\\

include? DFID_HALFMONOSAMPLE function_ids.j

anew task-halfmonosample.ins
dspp-echo off
_RED
dspp{
ins{

DFID_HALFMONOSAMPLE dspp-function-id !
$" halfmonosample.dsp" ins-name !

ALLOC.INFIFO  InFIFO
ALLOC.IMEM    Toggle       \ are we on or between samples
ALLOC.IMEM    PrevSample   \ previous for interpolation, expanded to 16 bits
ALLOC.IMEM    NewSample    \ hold sample from FIFO
ALLOC.OUTPUT Output
0 $ 7FFF $ 7FFF ALLOC.KNOB Amplitude
21 ALLOC.TICKS

\	InFIFO FIFOSTATUS+ _A #$ 0000 _+	\ address of FIFO status
\	_BEQ: @NODATA

\ -------------------------------------------
	Toggle _%A #$ 8000 _+   \ alternate between neg and pos
	_BMI: @GetNewSamp

\ play previous sample
	Output _A _= NewSample _A Amplitude _A _*
	_JUMP: @Done

_LABEL: @GetNewSamp

\ get new stereo 16 bit sample
	NewSample _A  _= InFIFO _A _TRL    \ get 16 bits from FIFO

\ interpolate between samples
	ACCUME 1 _>>>' _TRA             \ arithmetic shift right, new/2
	PrevSample _A #$ 4000 ACCUME _*+      \ prev/2 + new/2
	Output _A _= ACCUME Amplitude _A _*
	PrevSample _A NewSample _A _MOVE    \ save for next

_LABEL: @Done

\	_NOP
\ _LABEL: @NODATA
}ins
}dspp
