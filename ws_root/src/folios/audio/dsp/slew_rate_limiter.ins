\ @(#) slew_rate_limiter.ins 96/03/11 1.7
\ Slew rate limiter
\
\ Phil Burk
\ Copyright 3DO 1995
\ All Rights Reserved
\ Proprietary and Confidential

\\\	AUTODOC -class DSP -group Control_Signal -name slew_rate_limiter.dsp
\\\	Slew rate limiter.
\\\
\\\	  Description
\\\
\\\	    The Output of this instrument tracks its Input but can only change by a
\\\	    maximum amount. This is useful for slowing down control signals like
\\\	    Amplitude that can produce pops is changed too quickly. It can also be used
\\\	    to produce glide effects.
\\\
\\\	  Knobs
\\\
\\\	    Input - AUDIO_SIGNAL_TYPE_GENERIC_SIGNED
\\\	        -1.0 to +1.0, defaults to 0.0.
\\\
\\\	    Rate - AUDIO_SIGNAL_TYPE_GENERIC_SIGNED
\\\	        The output can change by no more than +/- Rate. in a single frame. -1.0
\\\	        to +1.0, defaults to 1.0.
\\\
\\\	  Outputs
\\\
\\\	    Output
\\\	        -1.0 to +1.0
\\\
\\\	  Resources
\\\
\\\	    Ticks: 19
\\\
\\\	    Code: 19 words
\\\
\\\	    Data: 3 words
\\\
\\\	  Implementation
\\\
\\\	    V28
\\\
\\\	  See Also
\\\
\\\	    envelope.dsp(@), integrator.dsp(@)

\ 960104 PLB fixed ticks and reversed subtract operands

include? DFID_SLEW_RATE_LIMITER function_ids.j

anew task-slew_rate_limiter.ins

_BULLDOG
dspp{
ins{

DFID_SLEW_RATE_LIMITER dspp-function-id !
$" slew_rate_limiter.dsp" ins-name !

$ 0000 KNOB_TYPE_RAW_SIGNED ALLOC.KNOB.M2 Input
$ 7FFF KNOB_TYPE_RAW_SIGNED ALLOC.KNOB.M2 Rate
ALLOC.OUTPUT Output
_IMPORT ALLOC.VARIABLE gScratch1   \ temp value, 1-N
19 ALLOC.TICKS

	gScratch1 _A _= Rate _A  $ 8000 _# _+ \ calc (n-1)
	Input _A  Output _A  _CLIP  _- \ calc delta , fixed 960104
\ slam against rails to restrict range of delta
	ACCUME   gScratch1 _A  _CLIP _-  \ -(n-1) = +(1-n)
	ACCUME   gScratch1 _A        _+  \ +(n-1) = -(1-n)
	ACCUME   gScratch1 _A  _CLIP _+  \ +(n-1) = -(1-n)
	ACCUME   gScratch1 _A        _-  \ -(n-1) = +(1-n)
	Output _%A ACCUME  _CLIP _+

}ins
}dspp
