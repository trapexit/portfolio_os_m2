\ @(#) sampler_8_f2.ins 96/03/11 1.12
\ sampler_8_f2 - Instrument Template for Stereo Sample Player without rate conversion
\
\ Author: Phil Burk
\ Copyright 1993 3DO
\ All Rights Reserved

\\\	AUTODOC -class DSP -group Sampled_Sound -name sampler_8_f2.dsp
\\\	Fixed-rate stereo 8-bit sample player.
\\\
\\\	  Description
\\\
\\\	    This instrument plays a stereophonic 8-bit sample at the instrument's
\\\	    execution rate (e.g., 44100 samples/sec).
\\\
\\\	  Knobs
\\\
\\\	    Amplitude - AUDIO_SIGNAL_TYPE_GENERIC_SIGNED
\\\	        0.0 to 1.0. Defaults to 1.0.
\\\
\\\	  Outputs
\\\
\\\	    Output - 2 parts
\\\	        Stereo output.
\\\
\\\	  Input FIFOs
\\\
\\\	    InFIFO
\\\
\\\	  Resources
\\\
\\\	    Ticks: 17
\\\
\\\	    Code: 14 words
\\\
\\\	    Data: 3 words
\\\
\\\	    FIFOs: 1
\\\
\\\	  Implementation
\\\
\\\	    V27
\\\
\\\	  Caveats
\\\
\\\	    8-bit audio sounds inherently BAD because there is very little information
\\\	    in each sample. Use one of the compressed data formats such as SQS2 for mono
\\\	    samples, or CBD2 for stereo samples, for better quality. The only reason to
\\\	    use 8-bit audio is if you don't have the 16-bit original recording.
\\\
\\\	  See Also
\\\
\\\	    sampler_8_f1.dsp(@), sampler_cbd2_f2.dsp(@)

include? DFID_SAMPLER_8_F2 function_ids.j

anew task-sampler_8_f2.ins

dspp-echo off
_BULLDOG
dspp{
ins{

DFID_SAMPLER_8_F2 dspp-function-id !
$" sampler_8_f2.dsp" ins-name !

DRSC_INFIFO_SUBTYPE_8BIT ALLOC.INFIFO.SUBTYPE  InFIFO
2 ALLOC.OUTPUT.ARRAY Output
$ 7FFF KNOB_TYPE_RAW_SIGNED ALLOC.KNOB.M2 Amplitude
17 ALLOC.TICKS

	InFIFO FIFOSTATUS+ _A   #$ 0002 _-  \ are there at least two samples in FIFO
	_BLT: NODATA                        \ if not forget it

	Output 0 PART+ _A _= InFIFO _A Amplitude _A _*	\ direct from FIFO to Mixer
	Output 1 PART+ _A _= InFIFO _A Amplitude _A _*	\ direct from FIFO to Mixer

_LABEL: NODATA

}ins
}dspp
