\ @(#) square.ins 96/05/06 1.17
\ $Id: square.ins,v 1.5 1995/01/20 20:20:23 peabody Exp $
\ Square wave
\
\ Copyright 1994 3DO
\ Phil Burk

\\\	AUTODOC -class DSP -group Sound_Synthesis -name square.dsp
\\\	Square wave generator.
\\\
\\\	  Description
\\\
\\\	    Square wave generator. It has a woody, clarinet-like sound.
\\\
\\\	  Knobs
\\\
\\\	    Frequency - AUDIO_SIGNAL_TYPE_OSC_FREQ
\\\	        Oscillator frequency in Hertz. Range is -22050.0 to +22050.0. Default is
\\\	        440.0.
\\\
\\\	    Amplitude - AUDIO_SIGNAL_TYPE_GENERIC_SIGNED
\\\	        -1.0 to +1.0, defaults to 1.0.
\\\
\\\	  Outputs
\\\
\\\	    Output
\\\	        -1.0 to +1.0
\\\
\\\	  Resources
\\\
\\\	    Ticks: 13
\\\
\\\	    Code: 12 words
\\\
\\\	    Data: 4 words
\\\
\\\	  Implementation
\\\
\\\	    V21
\\\
\\\	  See Also
\\\
\\\	    pulse.dsp(@), sawtooth.dsp(@), triangle.dsp(@), square_lfo.dsp(@)

decimal
include? DFID_SQUARE function_ids.j

anew task-square.dsp

_BULLDOG
dspp{
ins{

DFID_SQUARE dspp-function-id !
$" square.dsp" ins-name !

$ 8000 440 22050 */ KNOB_TYPE_OSC_FREQ ALLOC.KNOB.M2 Frequency
$ 7FFF KNOB_TYPE_RAW_SIGNED ALLOC.KNOB.M2 Amplitude
ALLOC.OUTPUT Output
ALLOC.VARIABLE Phase

13 ALLOC.TICKS

	Phase _%A Frequency _A _+
	_BLE:	@Negative

	Output _A Amplitude _A _MOVE    \ positive output
	_JUMP: @Done

_LABEL: @Negative
	Output _A _= Amplitude _A _NEG     \ negative output

_LABEL: @Done
_ENDEXT

}ins
}dspp

