\ @(#) triangle_lfo.ins 96/05/14 1.20
\ $Id: triangle_lfo.ins,v 1.7 1995/01/20 21:27:52 peabody Exp $
\ Instrument Template for Triangle LFO
\ Triangle waves look like /\/\/\/\/\/\
\
\ Phil Burk
\ Copyright 3DO 1993
\ All Rights Reserved
\ Proprietary and Confidential

\\\	AUTODOC -class DSP -group Control_Signal -name triangle_lfo.dsp
\\\	Triangle wave Low-Frequency Oscillator.
\\\
\\\	  Description
\\\
\\\	    This instrument is a triangle wave generator that uses extended precision
\\\	    arithmetic to give lower frequencies than triangle.dsp(@). It also has
\\\	    better resolution at the same frequency. The frequency of this instrument is
\\\	    256 times lower than its corresponding high frequency version. It is useful
\\\	    as a modulation source for controlling other instruments, or for bass
\\\	    instruments.
\\\
\\\	    This instrument is reset to the zero crossing on the rising side of the
\\\	    triangle when started.
\\\
\\\	  Knobs
\\\
\\\	    Frequency - AUDIO_SIGNAL_TYPE_LFO_FREQ
\\\	        Oscillator frequency in Hertz. Ranges from -86.1 Hz to +86.1 Hz.
\\\	        Default is 17.2 Hz.
\\\
\\\	    Amplitude - AUDIO_SIGNAL_TYPE_GENERIC_SIGNED
\\\	        -1.0 to +1.0, defaults to 1.0.
\\\
\\\	  Outputs
\\\
\\\	    Output
\\\	        -1.0 to +1.0
\\\
\\\	  Resources
\\\
\\\	    Ticks: 24
\\\
\\\	    Code: 20 words
\\\
\\\	    Data: 5 words
\\\
\\\	  Implementation
\\\
\\\	    V21
\\\
\\\	  See Also
\\\
\\\	    pulse_lfo.dsp(@), sawtooth_lfo.dsp(@), square_lfo.dsp(@), triangle.dsp(@)

\ Generate a sawtooth wave then use phase conversion to get a triangle.
\  sawtooth   => triangle
\    -1.0     => -1.0
\     0.0     =>  0.9999
\     0.9999  => -1.0

include? DFID_TRIANGLE_LFO function_ids.j

anew task-triangle_lfo.ins

_BULLDOG
dspp{
ins{

DFID_TRIANGLE_LFO dspp-function-id !
$" triangle_lfo.dsp" ins-name !
    \ initialize instrument

$ 8000 2 10 */ KNOB_TYPE_LFO_FREQ ALLOC.KNOB.M2 Frequency
$ 7FFF KNOB_TYPE_RAW_SIGNED ALLOC.KNOB.M2 Amplitude
ALLOC.OUTPUT Output

\ phase accumulator - output rectified and translated form
\ init at start to 0 on the rising side
$ -4000 DRSC_AT_START ALLOC.VARIABLE.DEFAULT PhaseHigh
$     0 DRSC_AT_START ALLOC.VARIABLE.DEFAULT PhaseLow

24 ALLOC.TICKS
\ use double precision math for phase accumulator
	Frequency _A 8 _<<' _TRA                    \ extract low bits
	PhaseLow _%A ACCUME _+                      \ add low phase
	PhaseHigh _A _+C                            \ carry bit
	PhaseHigh _A _= Frequency _A $ 0080 _#   ACCUME  _*+

\ Rectify sawtooth -> triangle wave with end points at -1.0 ($80000) and 0.0 ($FFFFF)
	_BMI: NegPhase
	ACCUME _++                      \ avoid pop when s=0
	ACCUME _NEG
_LABEL: NegPhase

\ Normalize triangle
	#$ 4000 ACCUME 1 _<<' _+        \ triangle = ( 0.5 + -ABS(sawtooth) ) * 2

\ Scale for output
	Output _A _= ACCUME Amplitude _A _*

_ENDEXT

}ins
}dspp
